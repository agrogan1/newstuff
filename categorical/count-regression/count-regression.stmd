% Count Regression
% Andy Grogan-Kaylor
% `s c(current_date)`

---
geometry: margin=1 in
---

# Key Concepts and Commands

<style>body {max-width: 90%;} h1 {color: #00274C;} h2 {color: #2F65A7} blockquote {border-left: 5px solid #ffcb05; margin: 1.5em 10px; padding: 0.5em 10px;}</style>

* formulas are our friends

$F(y) = \beta_0 + \beta_1 x_1 + ...$

$y = \ln(count): 1, 2, 3, \text{etc.}$

* Think about IRR's, predicted counts, non-linearity

* So much of categorical data analysis depends upon arguments for "functional form". When do we think these arguments are valid?

# The Count

![The Count and Friends](../images/Sesame-Street-optimize.gif){ width=20% }

# Historical Examples of Count Data ðŸŽ â˜Žï¸ ðŸ¥

* ðŸŽ Deaths from horsekicks in the Prussian Army
* â˜Žï¸ Calls to a call center (business, crisis hotline, etc.) 
* ðŸ¥ Arrivals at the Emergency Room 

From the Stata documentation: 

> "Poisson regression fits models of the number of occurrences (counts) of an event. The Poisson distribution has been applied to diverse events, such as the number of soldiers kicked to death by horses in the Prussian army (von Bortkiewicz 1898); the pattern of hits by buzz bombs launched against London during World War II (Clarke 1946); telephone connections to a wrong number
(Thorndike 1926); and disease incidence, typically with respect to time, but occasionally with respect to space."


# Other Canonical Examples of Count Data ðŸŒ³ðŸ¤’

* ðŸŒ³ðŸŒ³ðŸŒ³ Plants / trees in a field
* ðŸ¤’ðŸ¤’ðŸ¤’ Cases of disease / unit area

# Poisson Distribution

## Theorizing about the Poisson ðŸŸðŸŸðŸŸ ðŸŒ²ðŸŒ²ðŸŒ²

> The Poisson distribution is a modified form of the binomial distribution that is useful for the analysis of phenomena characterized by discrete, rare events.  For example, in a study of the distribution of a rare plant among a number of quadrats, a majority of plots may contain no specimens, a smaller number a single plant, and still smaller numbers two, three, or more plants.  If a single plant per quadrat is expected, the mean Âµ = 1 and the "0" and "1" classes occur at 37% each, the "2" class at 18%, the "3" class at 6%, and larger classes take up the remaining 2%.  The characteristic test for a Poisson is that the variance equals the mean, which in the plant example means that the rare plant is randomly distributed.  Note, that, In the distributions above, as the mean $\mu$ increases towards 10, the distribution approaches normality.

> The Poisson may be used to evaluate whether events occur independently in time as well as space.  In a classic example, Bortkiewicz (1898) studied the distribution of 122 men kicked to death by horses among ten Prussian army corps over 20 years. In most years in most corps, no one dies from being kicked; in one corp in one year, four men were kicked to death. Does this mean something was amiss in this particular corp?  Analysis indicates that the observed frequencies conform quite closely to the expected Poisson frequencies, and that the mean and variance are almost identical, as expected.  The corp was just "unlucky" in the sense that it is in the extreme tail of an ordinary run of events.

[http://www.mun.ca/biology/scarr/smcPoisson_distributions.html](http://www.mun.ca/biology/scarr/smcPoisson_distributions.html)

## Reprise of Normal Distribution

Normal distribution:

$$
    P(x) = \frac{1}{\sigma \sqrt{2 \pi}} e^{-(x-\mu)^2/(2 \sigma^2)}
$$

Standardized Normal Distribution:

$$
    P(z) = \frac{1}{\sqrt{2 \pi}} e^{-z^2/2}
$$

2 parameters:

$$
    x \sim N(\mu,\sigma)
$$

    clear all // clear all for simulated data

    set obs 1000 // number of observations
	
	generate x = rnormal() // normally distributed random variable
	
	histogram x, title("Normally Distributed Random Variable") scheme(michigan)
	
	graph export myhistogram.png, width(500) replace

![histogram of random normal variable](myhistogram.png)

## Poisson Distribution
 

$$
    P(Y=y) = e^{-\lambda} \frac{\lambda^y}{y!}
$$

> <span style="background-color: #FFFF00">A Poisson with large lambda looks very similar to a normal distribution.</span>

    generate w = rpoisson(.5)
	
	histogram w, title("Poisson Distributed Random Variable") scheme(michigan)
	
	graph export myhistogram2.png, width(500) replace

![histogram of random Poisson variable](myhistogram2.png)

## Poisson is the Natural Form for Observations Distributed in Time or Space ðŸ›ï¸ðŸ›ï¸ðŸ›ï¸ â³â³â³
$$ 
    x \sim Poisson(\lambda)
$$

> $\lambda$ is both mean and variance.

    clear all
	 	
    set obs 20

    generate field = _n // field number

    generate mycount = rpoisson(1)

    expand mycount // create individual observations

    generate x = runiform(1,10) // random x coordinate

    generate y =runiform(1,10) // random y coordinate
	
	generate mylabel = "ðŸ¦†"
	
	twoway scatter y x, ///
	by(field, title("Randomly Located Ducks")) ///
	mlab(mylabel) mlabsize(vlarge) ///
	msymbol(none)  ///
    legend(order(1 "ðŸ¦† Duck")) ///
	scheme(michigan) plotr(fcolor(olive_teal))

	graph export ducks.png, width(1000) replace
	
![Randomly Located Ducks](ducks.png){ width=50% }

	generate t = runiform(1,10) // random time coordinate
	
	generate y2 = runiform(1,2) // random y coordinate
	
	generate mylabel2 = "ðŸ”¥"
	
	generate timeperiod = field
	
	twoway scatter y2 t, ///
	by(timeperiod, title("Forest Fires At Random Times", size(vhuge)) cols(10)) ///
	ytitle("", size(zero)) ylabel(none) xtitle("", size(zero)) xlabel(none) ///
	subtitle(, size(vhuge)) ///
	mlab(mylabel2) mlabsize(vhuge) ///
	msymbol(none)  ///
    legend(order(1 "ðŸ”¥ Forest Fire")) ///
	scheme(michigan) plotr(fcolor(gs14)) ///
	xsize(5) ysize(1)

	graph export fires.png, width(1000) replace
	
![Forest Fires At Random Times](fires.png){ width=70% }

# Poisson Regression 

## National Survey of Children's Health (2018)

The data are an extract of the *National Survey of Children's Health, 2018*. The data contain information on children's exposure to various *Adverse Childhood Experiences* (ACEs) and their demographic characteristics.

    clear all
	
	use "../predict-and-margins-substantive-example/NSCH_ACES.dta", clear
	
	describe
	
## Generate Count of Aces
	
	egen acecount = anycount(ace*R), values(1) // generate count of ACES
	
    histogram acecount, discrete scheme(michigan)
	
	graph export myhistogram3.png, width(1000) replace
	
![Count of ACEs](myhistogram3.png){ width=50% }

## Poisson Regression
    	
    poisson acecount sc_sex i.sc_race_r i.higrade
		
## Incidence Rate Ratios
	
	poisson, irr
	
# Negative Binomial Distribution

## Over-Dispersion

Due to population heterogeneity (diversity, variation), variance may be $>$ mean. This is often empirically the case.

$$
    \text{var}(y) > \text{mean}(y)
$$

$$
    y \sim Poisson(\mu)
$$

$$
    \ln(\mu) = \beta_0 + \beta_1 x + \text{offset} + \text{dispersion} + etc.
$$

## Negative Binomial Regression
	
	nbreg acecount sc_sex i.sc_race_r i.higrade, irr

## Predicted Values

    predict yhat
	
	histogram yhat, scheme(michigan) 
	
	graph export myyhats.png, width(1000) replace
	
![Predicted Count of ACEs](myyhats.png){ width=50% }
   
## Exposure

In some data sets, we will have a *years exposed* or *time exposed* variable. It is important to control for this variable.

