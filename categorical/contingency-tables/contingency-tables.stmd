% Contingency Tables
% Andy Grogan-Kaylor
% `s c(current_date)`

---
geometry: margin=1 in
---

# Key Concepts and Commands

<style>body {max-width: 90%;} h1 {color: #00274C;} h2 {color: #2F65A7;} blockquote {border-left: 5px solid #ffcb05; margin: 1.5em 10px; padding: 0.5em 10px;}</style>

* Matrices of data
* Probabilities, risks, and odds
* $\chi^2$ Tests 
* `tabulate x y, row col chi2`

# Flipping Two Coins

![Coin Emoji From Apple](the-crazy-ones.jpg){ width=10% } 

![Coin Emoji From Apple](the-crazy-ones.jpg){ width=10% }


# Setup

    clear all
	
	set seed 3846
	
Good value labels are **key** here.
	
	label define nickel ///
	1 "heads for nickel" /// 
	0 "tails for nickel" // define value label
	
	label define quarter /// 
	1 "heads for quarter" /// 
	0 "tails for quarter" // define value label

    set obs 1000 // 1000 observations
	
	* curiously it takes around 1000 obs for the proportions
	* below to "take hold"
	
	generate nickel = rbinomial(1, .75) // unfair nickel
	
	generate quarter = rbinomial(1, .5) // fair quarter
	
	label values nickel nickel // assign value label
	
	label values quarter quarter // assign value label
	
# The Graph We Think We Want But Don't
	
	graph bar, over(nickel) scheme(burd) title(Nickel) name(nickel)
	
    graph bar, over(quarter) scheme(burd) title(Quarter) name(quarter)

    graph combine nickel quarter, title(Nickel And Quarter) scheme(burd)
	
    graph export unhelpfulgraph.png, width(500) replace

![A Graph That May Not Be That Helpful](unhelpfulgraph.png)

# Crosstabulation
	
	tabulate nickel quarter, row col
	
# Graphing (Mosaic Plot)

    * ssc install spineplot // mosaicplots (spineplots)
	
	* ssc install scheme-burd, replace // BuRd graph scheme
		
	spineplot nickel quarter, scheme(burd)
	
	graph export nickel-quarter.png, width(500) replace
	
![Mosaic Plot](nickel-quarter.png)

# Bar Chart

Does a bar chart work to visualize these relationships?

    graph bar, over(quarter) over(nickel) scheme(burd)
	
	graph export nickel-quarter-bar1.png, width(500) replace
	
![Bar Chart 1](nickel-quarter-bar1.png)

# Bar Chart (2)
	
Option `asyvars` adds a crucial color element.

    graph bar, over(quarter) over(nickel) scheme(burd) asyvars
	
	graph export nickel-quarter-bar2.png, width(500) replace
	
![Bar Chart 2](nickel-quarter-bar2.png)

# Horizontal Bar Chart

And `hbar` may improve legibility even more.

    graph hbar, over(quarter) over(nickel) scheme(burd) asyvars
	
	graph export nickel-quarter-bar3.png, width(500) replace

![Bar Chart 3](nickel-quarter-bar3.png)

# 1961 French Skiiers

    clear all

# Define Matrix

    matrix input FrenchSkiiers = (31, 109 \ 17, 122)

	matrix rownames FrenchSkiiers = Placebo AscorbicAcid
	
	matrix colnames FrenchSkiiers = Cold NoCold
	
	matrix list FrenchSkiiers
	
# Theme Music

[Polo And Pan on Spotify](https://open.spotify.com/track/260V7huyJrXnyYe0dFv2Fa)

# Try Making a Data Set From Matrix
	
	svmat FrenchSkiiers, name(count)
	
	list
	
# Enter Data By Hand

There are many alternative commands to do this, but the easiest way is using `edit`.

I have already done this. Note the structure of the data is different from above.

    use "FrenchSkiiers.dta", clear
	
	list // list the data
	
# Mosaic Plot

	spineplot Tx Outcome, scheme(burd)
	
	graph export FrenchSkiiers1.png, width(500) replace
	
![Mosaic Plot Attempt 1](FrenchSkiiers1.png)

# Mosaic Plot (2)

    spineplot Outcome Tx [fweight=Count], scheme(burd) // order matters to interpretability
   
    graph export FrenchSkiiers2.png, width(500) replace

![Mosaic Plot Attempt 2](FrenchSkiiers2.png)

# Definitions and Notation

## Counts

$\begin{matrix} c_{ij} & c_{ij} & c_{i\bullet} \\ c_{ij} & c_{ij} & c_{i\bullet} \\ c_{\bullet j} & c_{\bullet j} & c_{\bullet \bullet} \\ \end{matrix}$

## Probabilities

$\begin{matrix}p_{ij} & p_{ij} & p_{i\bullet} \\ p_{ij} & p_{ij} & p_{i \bullet} \\ p_{\bullet j} & p_{\bullet j} & p_{\bullet \bullet} \\ \end{matrix}$

# Terms

$p_{ij}$ are *joint* probabilities.

$p_{i \bullet}$ and $p_{\bullet j}$ are *marginal* probabilities.

$p_{ij} \mid p_{i \bullet}$ and $p_{ij} \mid p_{\bullet j}$ are *conditional* probabilities.

# Formulas 

## Counts

$\sum_{1}^{i} \sum_{1}^{j} c_{ij} = N$

## Probabilities

$\sum_{1}^{i} \sum_{1}^{j} p_{ij} = 1.0$

## Expected Probabilities $p$ and Counts $m$ or Frequencies

$p_{ij} = p_{i \bullet} p_{\bullet j}$

$m_{ij} = \frac{m_{i \bullet} m_{\bullet j}}{m_{\bullet \bullet}}$

> Observed counts are represented by $c$ while expected counts are represented by $m$.

# Fundamental Rule

$$\text{conditional = joint / marginal}$$

    tabulate Tx Outcome [fweight = Count], cell row col
	
	display 6.09 / 49.82
	
    display 17/139

# Independence (Robert Mare)

> If independence is true, then joint probabilities = products of marginal probabilities.

> That is, under independence, the conditional distribution equals the marginal distribution.

> Under independence, row membership provides no information about the column distribution; and column membership provides no information about the row distribution.

> Independence is a model, which is never exactly true in the real world.

# Observed vs. Expected

    tabulate Tx Outcome [fweight = Count]

    scalar N = 31 + 109 + 17 + 122

	scalar A = ((31 + 17)*(31 + 109)) / N // expected count

	scalar B = ((31 + 109)*(109 + 122)) / N // expected count

	scalar C = ((31 + 17) * (17 + 122)) / N // expected count

	scalar D = ((17 + 122) * (109 + 122)) / N // expected count
	
	matrix FS = (A, B \ C, D) // matrix of expected values
	
	matrix rownames FS = Placebo AscorbicAcid // rownames
	
	matrix colnames FS = Cold NoCold // column names
	
	matrix list FS

# Chi-Square Test

$\chi^2 = \Sigma \frac{(O-E)^2}{E}$

    scalar chisquare = (31 - 24.086022)^2 / 24.086022 + /// 
    (109 - 115.91398)^2 / 115.91398 + ///
    (17 - 23.913978)^2 / 23.913978 + /// 
	(122 - 115.08602)^2 / 115.08602
	
	scalar list chisquare

# Compare With Tabulate

    use "FrenchSkiiers.dta", clear
	
	tabulate Tx Outcome [fweight = Count], row col chi2

# Risk Differences and Risk Ratios (Relative Risk)

Following Viera, 2008:

$\begin{bmatrix}a & b \\ c & d\end{bmatrix}$


|             | Develop Outcome   | Do Not Develop Outcome   |
|-------------|:-----------------:|:------------------------:|
| Exposed     | a                 | b                        |
| Not Exposed | c                 | d                        |


$R = \frac{a}{a+b}$ (in Exposed)

$RR =\frac{\text{risk in exposed}}{\text{risk in not exposed}} = \frac{a/(a+b)}{c/(c+d)}$ 

# Calculating a Risk Ratio

    tabulate Outcome Tx [fweight = Count]

	tabulate Outcome Tx [fweight = Count], col

	display 31/140

	display 17/139

	display (17/139) / (31/140)
	
    csi 17 31 122 109 // also has an intuitive dialog box

# Odds Ratios 

|             | Develop Outcome   | Do Not Develop Outcome   |
|-------------|:-----------------:|:------------------------:|
| Exposed     | a                 | b                        |
| Not Exposed | c                 | d                        |

$OR =$

$\frac{\text{odds that exposed person develops outcome}}{\text{odds that unexposed person develops outcome}}$

$= \frac{\frac{a}{a+b} / \frac{b}{a+b}}{\frac{c}{c+d} / \frac{d}{c+d}} = \frac{a/b}{c/d} = \frac{ad}{bc}$

# Properties of the Odds Ratio (Robert Mare)

> In general for the 2 X 2 Table, 

$0 < OR < 1$ 

> indicates that one row is less likely to make the first response than the other row.

$1 < OR < \infty$

> indicates that one row is more likely to make the first response than the other row.

# Calculate Odds Ratio

    tabulate Tx Outcome [fweight = Count]

    display (17 * 109)/(122 * 31)
	
	csi 17 31 122 109, or // also has an intuitive dialog box

 
 
 
 
