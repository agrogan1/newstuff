% Survival Analysis and Event History
% Andy Grogan-Kaylor
% `s c(current_date)`

---
geometry: margin=1 in
---

<style>h1 {color: #00274C;} h2 {color: #2F65A7;} blockquote {border-left: 5px solid #ffcb05; margin: 1.5em 10px; padding: 0.5em 10px;}</style>

# Introduction

> "Survival analysis is a key technique in data-driven decision-making, which is now central to public interest because of COVID-19. Applying the correct technique for the specific question at hand is crucial for credible public health inferences. If you are interested in assessing how a risk factor or a potential treatment affects the progression of a disease—such as how long a patient takes to recover—then survival analysis techniques come into play. Survival analysis deeply respects the ultimate source of its data, often the disease experience or even the life and death of human patients. It seeks to exploit every last drop of information that this experience can render for saving lives—in particular, not only whether patients survived, but how long, and why. And it strives to do so with minimal assumptions, so that the data are truly driving the decision."

—SAS Corporation

# Key Concepts

> WHO CARES how we measure time? Isn't it self-evident?

* Implementations differ; formulas are our friends

* $h(t) \sim x1 + x2 + \text{etc.} ...$: formula (effect on hazard)

# The "Hospital Bed Problem"

* Imagine a *Hypothetical Hospital* 

* Imagine that there are 52 patients *total*.

* 51 of the patients are *long term patients*, who each stay for *1 year*. 

* 1 of the patients is a *short term patient*, who stays for *1 week*.

> Is this a hospital that serves mostly long-term, or short term patients?

    clear all

    set obs 52 // 52 hypothetical obervations

    generate id = _n // set id = to observation #
	
	generate weeks = 52
	
	replace weeks = 1 if id == 52
	
	twoway (scatter id weeks if weeks == 52, msize(small)) /// staying 52 weeks
	(scatter id weeks if weeks == 1, msize(small)), /// staying 1 week
	title("Hypothetical Hospital") ///
	legend(on order(1 "long term" 2 "short term")) ///
	ylabel(1(1)52, labels labsize(tiny) angle(horizontal) noticks nogrid) /// 
	scheme(michigan)
	
	graph export hospital_bed_problem.png, width(500) replace

![Illustration of Hospital Bed Problem](hospital_bed_problem.png)

# How To Measure Length of Stay (1)

    clear all

    set obs 25 // 25 hypothetical obervations

    generate id = _n // set id = to observation #

    generate time = runiform(1, 100) // random times

    generate censored = time > 75 // censored if time > 75

    twoway (scatter id time if censored == 0) ///
    (scatter id time if censored == 1), ///
    title("Hypothetical Timing of Events") ///
    subtitle("Think About Different Kinds of Events") ///
    note("Study Ends At Time 75") ///
    legend(on order(1 "not censored" 2 "censored")) ///
    xline(75, lcolor("red")) /// censoring line at 75
    ylabel(1(1)25, labsize(vsmall) angle(horizontal)) /// lines from 1 to 25
    scheme(michigan)

	graph export timing_of_events.png, width(500) replace

![Timing Of Events](timing_of_events.png)

## Animated

See [times-events-and-censoring.html](./times-events-and-censoring.html)

# How To Measure Length of Stay (2)

$$
    \ln(\frac{P(event)}{1-P(event)}) = \beta_0 + \beta_1 x_1 + \beta_2 x_2 + e_i
$$

* Event happened within a specified time (yes/no)
   - Statistically accurate, but we lose information on *when* the event happened.
   - Statistically *less efficient*.
   
$$
    \text{time} = \beta_0 + \beta_1 x_1 + \beta_2 x_2 + e_i
$$

* Time until Event
  - What to do with events that haven't happened yet? (Censoring)
  - Code as `NA`. Loss of information. Possible bias.
  - Code as 0. Possible bias. They might happen at some point.
  - Code as `time of censoring`. Possible bias.  They might never happen. They might happen much later.

# A Policy Example (Welfare Reform, 1996)

From LaDonna Pavetti (1995)

* time in months
* new entrants (percent)
* all current recipients at a point in time (percent)

    clear all
	
	use Pavetti.dta
	
	list, abbreviate(25) // list out the data
	
	graph bar (asis) all_current_recipients, /// this particular set of options was difficult to figure out!
	over(time) ///
	title("All Current Recipients") ///
	sub("By Months On Caseload") ///
	ytitle("percent") ///
	scheme(michigan)
	
	graph export all_current_recipients.png, width(500) replace

![All Current Recipients by Months on Caseload](all_current_recipients.png)

# Welfare Reform (2)

    graph bar (asis) new_entrants, ///
	over(time) ///
	title("New Recipients") ///
	sub("By Months On Caseload") ///
	ytitle("percent") ///
	scheme(michigan)
	
    graph export new_recipients.png, width(500) replace

![New Recipients by Months on Caseload](new_recipients.png)

# Musicians and Mortality (1)

![Music To Die For](music-to-die-for.png){width=50%}

# Musicians and Mortality (2)

![Musician Mortality](musician_mortality.jpeg){width=50%}

# Cox Proportional Hazards Model 

# Formula

$h(t)$ the rate of occurrence.

$$
    h(t) = h_0(t)e^{\beta_1 x1 + \beta_2 x_2 + etc.}
$$

> We don't directly estimate the hazard, but estimate the effect of covariates on the hazard.

> The event (birth, death, program entry, program departure) is coded as 1, so we are estimating the association of the covariates with event occurrence.

# Cox Proportional Hazards Model in Stata

> Using a data set referenced frequently in Stata `help` and Stata YouTube Videos

    clear all

    webuse drugtr // demonstration data set from Stata

## Setup of Data
	
	stset // show st setup of data
	
## Kaplan-Meier Survivor Function (per Gabriela Ortiz, Stata)

$$
    S(t)=Pr(T>t)
$$
	
	sts graph, scheme(michigan) // Kaplan-Meier Survivor Function
	
## Cox Proportional Hazards Model
	
    stcox age drug // run Cox Proportional Hazards Model
	
## Graph Survival Curves
	
    stcurve, survival scheme(michigan) // survival curve
	
	graph export survival1.png, width(500) replace

![Survival Curve](survival1.png)
	
    stcurve, survival at1(drug=0) at2(drug=1) scheme(michigan) // survival curve by group
	
	graph export survival2.png, width(500) replace

![Survival Curve by Drug Group](survival2.png)

# Proportional Hazards Assumption

    estat phtest // formal test of PH assumption

    stphplot, by(drug) scheme(michigan) // graphical test of PH assumption
	
	graph export ph.png, width(500) replace
	
![Graphical Assessment of Proportional Hazards Assumptions](ph.png)	

# Unobserved Heterogeneity



