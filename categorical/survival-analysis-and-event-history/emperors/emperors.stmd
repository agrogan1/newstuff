% Event History Analysis With Roman Emperor's Data
% Andy Grogan-Kaylor
% `s c(current_date)`

---
geometry: margin=1 in
---

<style>body {max-width: 90%} h1 {color: #00274C;} h2 {color: #2F65A7;} blockquote {border-left: 5px solid #ffcb05; margin: 1.5em 10px; padding: 0.5em 10px;} code {color: red;}</style>

# Introduction

This example uses data on the ages of death of Roman Emperors. Sources for this data are unclear, but it appears that the original source is [http://www.roman-emperors.org/](http://www.roman-emperors.org/) via [https://github.com/rfordatascience/tidytuesday/tree/master/data/2019/2019-08-13](https://github.com/rfordatascience/tidytuesday/tree/master/data/2019/2019-08-13).

# Get Data

    clear all

    import delimited "https://raw.githubusercontent.com/agrogan1/newstuff/master/categorical/survival-analysis-and-event-history/emperors/emperors.csv"

# Data Wrangling

Remember that Stata works with dates by converting them to the *number of days since January 1, 1960*.

    * we can't use the date() function 
    * because it does not work
    * with dates prior to 100AD

    * generate birthdate = date(birth, "YMD")

    * generate deathdate = date(death, "YMD")

    generate birthyear = real(substr(birth, 1, 4)) // convert first 4 characters to real number

    generate deathyear = real(substr(death, 1, 4)) // convert first 4 characters to real number

    * browse name name_full birth birthyear death deathyear

    generate age = deathyear - birthyear

    * need to recalculate age for those born in BCE

    encode cause, generate(causeNUMERIC) // numeric version of cause of death

    codebook causeNUMERIC if age != . // show values of causeNUMERIC for non missing ages
	
	encode rise, generate(riseNUMERIC) // numeric version of cause of death
	
	codebook riseNUMERIC

# `stset` The Data <a id="stset"></a>

We need to `stset` the data so that Stata knows that this is survival data with special characteristics relevant to survival analysis. For those of you have used other commands that attach special characteristics to the data, this is similar to using `svyset` for complex survey data, `xtset` for panel data, or even to the `mi` suite of commands for multiple imputation.

The most commonly used syntax is something like `stset timevar, failure(failvar) id(id)` [^failvar]

[^failvar]: `failvair` is often something like `died`.

> There are many ways to specify `failvar`, we outline the most straightforward. Consult Stata help for your exact situation.

    stset age // stset the data

# Kaplan-Meier Survivor Function (per Gabriela Ortiz, Stata)

## Overall Survival Function

$$S(t)=Pr(T>t)$$

    sts graph, scheme(michigan)
	
	graph export mysurvival0.png, width(1000) replace

![Survival Curve](mysurvival0.png){width=50%}

## Survival Function by Cause of Death

    sts graph, by(causeNUMERIC) scheme(michigan) // survival curve by cause of death
	
    graph export mysurvival1.png, width(1000) replace
	
![Survival Curve by Cause of Death](mysurvival1.png){width=50%}

As an opportunity to take a closer look at the graph, we take a look at *cause of death* by age for those who *died in battle*.
	
	tabulate age causeNUMERIC if causeNUMERIC == 3
	
We can then work to make the legend more informative.

    sts graph, by(causeNUMERIC) scheme(michigan) ///
    legend(pos(6) col(2) order(1 "Assasination" 2 "Captivity" 3 "Died in Battle" /// 
    4 "Execution" 5 "Natural Causes" 6 "Suicide" 7 "Unknown")) // survival curve w better legend
	
    graph export mysurvival2.png, width(1000) replace

![Survival Curve With Better Legend](mysurvival2.png){width=50%}

# Cox Proportional Hazards Model

## Formula for the Hazard <a id="hazard-formula"></a>

$h(t)$ the rate of occurrence.

$$
    h(t) = \lim_{\delta\to\infty} \frac{\text{probability of having an event before time } t + \delta}{\delta}
$$ 

This definition per Johnson & Shih (2007). 

$$
    h(t) = h_0(t)e^{\beta_1 x1 + \beta_2 x_2 + etc.}
$$

> We don't directly estimate the hazard, but estimate the effect of covariates on the hazard.

## Estimate the Cox Proportional Hazards Model

    stcox ib5.causeNUMERIC ib5.riseNUMERIC // Cox model

    stcurve, survival at(causeNUMERIC=(1(1)7)) ///
    scheme(michigan) // basic survival curve by causeNUMERIC

    graph export mycox1.png, width(1000) replace

![Survival Curve From Cox Model](mycox1.png){width=50%}

    stcurve, survival ///
    at(causeNUMERIC=(1(1)7)) ///
    caption("Roman Emperors Data") ///
    legend(order(1 "Assasination" 2 "Captivity" 3 "Died in Battle" /// 
    4 "Execution" 5 "Natural Causes" 6 "Suicide" 7 "Unknown")) ///
    scheme(michigan) // more nicely formatted survival curve

    graph export mycox2.png, width(1000) replace

![Survival Curve From Cox Model](mycox2.png){width=50%}

## Proportional Hazards Assumption <a id="ph-assumption"></a>

    estat phtest, detail // formal test of PH assumption

    stphplot, by(causeNUMERIC) scheme(michigan) // graphical test of PH assumption
	
	graph export ph.png, width(1000) replace
	
![Graphical Assessment of Proportional Hazards Assumptions](ph.png){width=50%}

## Correcting For Violations of the Proportional Hazards Assumption

Had the proportional hazards assumption been violated, we could correct for this violation in one of two ways:

1. Estimating an interaction of the time variable (in this case `age`) with the variable violating the assumption.

e.g. `stcox ib5.causeNUMERIC age#ib5.riseNUMERIC`. 

> Note: In this relatively small sample this command fails to converge, perhaps because of sample size; or perhaps because there is no underlying violation of the proportional hazards assumption.

2. Using the `, strata(varname)` option to *stratify* on the variable violating the assumption.

> Note that the command below provides results, but does not provide parameter estimates for the variable on which we are stratifying, `riseNUMERIC`. 

    stcox ib5.causeNUMERIC, strata(riseNUMERIC)

# References

Johnson, L. L., & Shih, J. H. (2007). CHAPTER 20 - An Introduction to Survival Analysis (J. I. Gallin & F. P. Ognibene, eds.). https://doi.org/https://doi.org/10.1016/B978-012369440-9/50024-4


