% Causal Modeling With GSS Data Using Multiple Approaches
% Andy Grogan-Kaylor
% `s c(current_date)` `s c(current_time)`

---
geometry: margin=1 in
---

<style>h1 {color: #00274C;} h2 {color: #2F65A7;} blockquote {border-left: 5px solid #ffcb05; margin: 1.5em 10px; padding: 0.5em 10px;}</style>

# Research Question

> What is the *possibly causal* association of *education* with *job satisfaction*, while accounting for factors that may possibly have an association with *level of education*?

# Causality 

A variable $x$ can only be considered to have *causal* association with $y$ if the following conditions are met (Holland, 1986):

1. $x$ is correlated with $y$.
2. $x$ precedes $y$ in time order.
3. The association between $x$ and $y$ can not be accounted for by any third variable $z$.

Hence, for this particular data, we are exploring: 

> What happens to the association of *education* and *job satisfaction* when we control for possible confounding variables $z$ using various statistical strategies?

# *To Be Added To Each Analysis*

* Assumptions
* Equation
* Stata Command
* Conclusion

# Setup

    clear all

    cd "/Users/agrogan/Desktop/newstuff/causal-modeling"

## Get Data

    use "GSS_panel2010w123_R6 - stata.dta", clear

## ID Variable

    generate ID = id_1

## Keep Only Relevant Variables 

    keep ID satjob_? educ_? race_? incom16_?

## Describe Data

    describe

## Codebook For Selected Variable(s)

    codebook satjob_3

# Analyses Relying On Wide Data

## Correlation

    pwcorr satjob_3 educ_3, sig

## Regression With 1 Independent Variable

    regress satjob_3 educ_3

## Regression With Multiple Independent Variables

    regress satjob_3 educ_3 i.race_3 incom16_3 

## Propensity Score 

### Data Wrangling Since Propensity Score Requires a Binary Treatment Variable

    generate twelve_years_3 = educ_3 >= 12 // 12 or more years of education
	
    generate twelve_years_2 = educ_2 >= 12 // 12 or more years of education

    generate twelve_years_1 = educ_1 >= 12 // 12 or more years of education
	
	label variable twelve_years_3 "12 or more years of education"
	
	label variable twelve_years_2 "12 or more years of education"

	label variable twelve_years_1 "12 or more years of education"

### Propensity Score Analysis

    teffects psmatch (satjob_3) (twelve_years_3 incom16_3 i.race_3)

### Assess Balance of Propensity Score Model [^Cuartas]

[^Cuartas]: With many thanks to Jorge Cuartas for the ideas for earlier versions of this code.

    tebalance summarize
	
    tebalance density, scheme(michigan)

	graph export mydensity.png, width(500) replace

![Density Plot of Propensity Score](mydensity.png)

## Cross Lagged Regression

    sem (satjob_3 <- twelve_years_2 incom16_2) ///
    (twelve_years_3 <- satjob_2 incom16_2), ///
    cov(e.satjob_3*e.twelve_years_3)

# Analyses Relying On Long Data

## Reshape The Data

    reshape long satjob_ educ_ twelve_years_ incom16_ race_, i(ID) j(wave)
	
## Clean Up Variable Names

	rename satjob_ satjob
	
	rename educ_ educ
	
	rename incom16_ incom16
	
	rename race_ race
	
	rename twelve_years_ twelve_years

## Multilevel Model

    mixed satjob wave educ incom16 i.race || ID: 

## Fixed effects regression

    xtreg satjob wave educ incom16 i.race, i(ID) fe

## "Hybrid" Model

> The contention here is that the *between person* coefficient replicates the effect of the fixed effects regression coefficient while the *within person* coefficient is simultaneously estimated.

### Generate Within And Between Variables

    bysort ID: egen educ_mean = mean(educ)
	
	generate educ_deviation = educ - educ_mean
	
### Estimate The Model
	
	mixed satjob wave educ_mean educ_deviation incom16 i.race || ID:

## Difference In Difference Model

# Combinations of Models

## Difference In Difference + Propensity Scores

## Cross-Lagged Model With Fixed Effects

# References

Holland, P. W. (1986). Statistics and Causal Inference. *Journal of the American Statistical Association*, 81(396), 945â€“960. https://doi.org/10.1080/01621459.1986.10478354
	
	



